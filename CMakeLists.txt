cmake_minimum_required(VERSION 3.15)


set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

set(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)
include(SetupDealII)

# This needs to be called before project is called and before we setup the other
# TPL and adamantine
DEAL_II_INITIALIZE_CACHED_VARIABLES()

project(adamantine LANGUAGES CXX VERSION 0.1.0)

include(SetupTPLs)
include(SetupAdamantine)

if (ADAMANTINE_ENABLE_CUDA)
  enable_language(CUDA)
endif()

add_subdirectory(application)
add_subdirectory(source)

option(ADAMANTINE_ENABLE_COVERAGE "Measure coverage" OFF)
if (ADAMANTINE_ENABLE_COVERAGE)
  include(CodeCoverage)
endif()

option(ADAMANTINE_ENABLE_TESTS "Build tests" OFF)
if (ADAMANTINE_ENABLE_TESTS)
  include(Testing)
  enable_testing()
  include(CTest)
  add_subdirectory(tests)
  add_test(NAME indent_code
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
    COMMAND ./indent
    )
endif()

# Provide "indent" target for indenting all the header and the source files.
add_custom_target(indent
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  COMMAND ./indent
  )

if (ADAMANTINE_ENABLE_DOCUMENTATION)
    add_subdirectory(doc)
endif()
